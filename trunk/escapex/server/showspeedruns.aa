let
    import "escape.aah"
    val p = cgigetstring ([pass], K "")
    val url = limit (?/, cgiurl())
    val n = stoi (head url)

    val (name, fname, minv) = 
        get escape : collection ( n ) : (name, fname, minv)

    (* some settings saved in a cookie *)
    val lastm = 1 = getcookien (LASTMCOOKIE, fn _ => 0)

in
    p seq ESCAPE-ADMINPASS otherwise die [wrong password];

    adminheader [Escape Server : Speedruns for [fname]];

    let 
        val levs = select (id, subdir, lev) 
                   from escape : levelrel
                   where col = {n}
                   
        fun prlev (lid, sd, lev) =
            let
                val (md, title, author, owner, nvotes,
                     difficulty, style, rigidity, cooked, solved) =
                    get escape : level (lev) : 
                    (md, title, author, owner, nvotes,
                     difficulty, style, rigidity, cooked, solved)

                val best =
                    (case (select (len, author) 
                           from escape : solution
                           where of = {lev}
                           order by len, date
                           limit {1}) of
                         nil => NONE
                       | (h :: _) => SOME h)
                         

                fun prbest (SOME (len, a)) = [[itos len] by [a]]
                  | prbest NONE = [--]
            in
                print 
                [<tr><td>[itos lev].</td>
                 <td>[disp title]</td>
                 <td>[disp author]</td>
                 <td>[prbest best]</td>];

                print [</tr>\n]
            end

    in
        print [<table>\n];
        print [<tr><th>#</th><th>title</th><th>author</th>
               <th>speedrun</th>\n];
        app (levs, prlev);
        print [</table>\n]
    end

end
