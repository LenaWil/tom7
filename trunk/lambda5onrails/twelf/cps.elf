
% The ML5 CPS language, its static and dynamic semantics,
% and its proof of safety.

cexp : type.    %name cexp C c.
% forward-declared:
% cval : type.    %name cval V v.
% ctyp : type.    %name ctyp A a.
% cvval : type.   %name cvval VV vv.

cat  : ctyp -> world -> ctyp.             %infix none 2 cat.
% since this is unary, we'll use iterated tuples and
% unit to implement multi-arg functions.
ccont  : ctyp -> ctyp.                      %postfix 8 ccont.
call : (world -> ctyp) -> ctyp.
cexists : (world -> ctyp) -> ctyp.
c& : ctyp -> ctyp -> ctyp.                 %infix none 9 c&.
% representation for a world
caddr : world -> ctyp.
cunit : ctyp.
% shamrock
csh : (world -> ctyp) -> ctyp.

% continuation expressions
cfst : cval -> (cval -> cexp) -> cexp.
csnd : cval -> (cval -> cexp) -> cexp.
clocalhost : (cval -> cexp) -> cexp.

clets : cval -> (cvval -> cexp) -> cexp.
cput : cval -> (cvval -> cexp) -> cexp.
cleta : cval -> (cval -> cexp) -> cexp.

% let is not necessary because of the natural let-style
% of continuation exps
cunpack : cval -> (world -> cval -> cexp) -> cexp.
cwapp : cval -> world -> (cval -> cexp) -> cexp.

% this is the translation of 'get'
cgo : world -> cval -> cexp -> cexp.
% continuations can end with a call to a function
ccall : cval -> cval -> cexp.
chalt : cexp.


% continuation values
cpair : cval -> cval -> cval.
cheld : world -> cval -> cval.
clam : (cval -> cexp) -> cval.
cconst : world -> cval.
cwlam : (world -> cval) -> cval.
cpack : world -> cval -> cval.
c1 : cval.
% inclusion of cvvals in cvals
cvalid : cvval -> cval.
% internalization into sh modality
ch : cvval -> cval.
% the only vval
cvv : (world -> cval) -> cvval.

% need to redefine this, too
cmobile : ctyp -> type.
%mode cmobile *A.

cmob_& : cmobile A -> cmobile B -> cmobile (A c& B).
cmob_addr : cmobile (caddr W).
cmob_at : cmobile (A cat W).
cmob_all : ({w} cmobile (A w)) -> cmobile (call A).
cmob_exists : ({w} cmobile (A w)) -> cmobile (cexists A).
cmob_sh : cmobile (csh A).


% ################
% Static Semantics
% ################

% Well-formed continuation expressions
cof : cexp -> world -> type.              %name cof WC wc.
%mode cof *C *W.

% values and valid values
% cofv : cval -> ctyp -> world -> type.     %name cofv WV wv.
%mode cofv *A *B *C.
% cofvv : cvval -> (world -> ctyp) -> type. %name cofvv WVV wvv.
%mode cofvv *A *B.

co_halt : cof chalt W.

co_go : cofv VW (caddr W') W ->
        cof C W' ->
        cof (cgo W' VW C) W.

co_put : cmobile A ->
	 cofv V A W ->
	 ({v} cofvv v ([w] A) ->
	    cof (N v) W) ->
	 cof (cput V N) W.

co_lets : cofv V (csh Af) W ->
	  ({v} cofvv v Af ->
	    cof (N v) W) ->
	  cof (clets V N) W.

co_leta : cofv V (A cat W') W ->
          ({v} cofv v A W' ->
            cof (N v) W) ->
          cof (cleta V N) W.

co_wapp : cofv V (call A) W ->
          ({v} cofv v (A W') W ->
             cof (N v) W) ->
          cof (cwapp V W' N) W.

co_unpack : 
          cofv V (cexists A) W ->
          ({w}{v} cofv v (A w) W ->
              cof (N w v) W) ->
          cof (cunpack V N) W.

co_localhost : ({v} cofv v (caddr W) W -> 
                    cof (K v) W) ->
               cof (clocalhost K) W.

co_call : cofv F (A ccont) W ->
          cofv V A W ->
          cof (ccall F V) W.

co_fst : cofv V (A c& B) W ->
         ({v}{ov : cofv v A W} cof (C v) W) ->
         cof (cfst V C) W.

co_snd : cofv V (A c& B) W ->
         ({v}{ov : cofv v B W} cof (C v) W) ->
         cof (csnd V C) W.

cov_unit : cofv c1 cunit W.

cov_pair : cofv V1 A W ->
           cofv V2 B W ->
           cofv (cpair V1 V2) (A c& B) W.

cov_held : cofv V A W' ->
           cofv (cheld W' V) (A cat W') W.

cov_lam : ({x} cofv x A W ->
               cof (M x) W) ->
          cofv (clam M) (A ccont) W.

cov_const : cofv (cconst W) (caddr W) W'.

cov_wlam : ({w} cofv (V w) (A w) W) ->
           cofv (cwlam V) (call A) W.

cov_pack : {A : world -> ctyp} % sometimes need this annotation
           cofv V (A W') W -> 
           cofv (cpack W' V) (cexists A) W.

cov_valid : cofvv VV Af ->
            cofv (cvalid VV) (Af W) W.

cov_ch : cofvv VV Af ->
         cofv (ch VV) (csh Af) W.

covv : ({w} cofv (VF w) (Af w) w) ->
       cofvv (cvv VF) Af.

% boring equality lemmas.
ceqtyp : ctyp -> ctyp -> type.
ceqtyp_ : ceqtyp A A.

% equality is preserved under constructors
ceqtyp_& : ceqtyp A A' -> ceqtyp B B' -> ceqtyp (A c& B) (A' c& B') -> type.
- : ceqtyp_& ceqtyp_ ceqtyp_ ceqtyp_.
%mode ceqtyp_& +A +B -C.

ceqtyp_cont : ceqtyp A A' -> ceqtyp (A ccont) (A' ccont) -> type.
- : ceqtyp_cont ceqtyp_ ceqtyp_.
%mode ceqtyp_cont +A -C.

ceqtyp_sh : ({w} ceqtyp (A w) (A' w)) -> ceqtyp (csh A) (csh A') -> type.
- : ceqtyp_sh ([w] ceqtyp_) ceqtyp_.
%mode ceqtyp_sh +A -C.

ceqtyp_all : ({w} ceqtyp (A w) (A' w)) -> 
	    ceqtyp (call A) (call A') -> type.
- : ceqtyp_all ([w] ceqtyp_) ceqtyp_.
%mode ceqtyp_all +A -B.

ceqtyp_exists : ({w} ceqtyp (A w) (A' w)) -> 
	    ceqtyp (cexists A) (cexists A') -> type.
- : ceqtyp_exists ([w] ceqtyp_) ceqtyp_.
%mode ceqtyp_exists +A -B.

ceqtyp_at : {W:world} ceqtyp A A' -> ceqtyp (A cat W) (A' cat W) -> type.
- : ceqtyp_at _ ceqtyp_ ceqtyp_.
%mode ceqtyp_at +W +A -B.


cofv_resp : cofv C A W -> ceqtyp A A' -> cofv C A' W -> type.
%mode cofv_resp +COF +EQ -COF'.

cofv_resp_ : cofv_resp D ceqtyp_ D.


% dynamic semantics. Actually much simpler than the
% dynamic semantics for the Internal Language.

% mobilize a value. only some can be mobilized.
% (this is like the 'get' operations below)
cmoval : world -> cval -> cvval -> type.
%mode cmoval *W *A *B.

cmoat : cmoval _ (cheld W' V) (cvv [_] cheld W' V).
cmopair : cmoval W V1 V1' -> cmoval W V2 V2' -> 
         cmoval W (cpair V1 V2) (cvv [_] cpair (cvalid V1') (cvalid V2')).
cmoall : ({w:world} cmoval W (V w) (V' w)) -> 
         cmoval W (cwlam V) (cvv [_] cwlam [w] (cvalid (V' w))).
cmoexists : cmoval W V V' -> cmoval W (cpack W' V) (cvv [_] cpack W' (cvalid V')).
cmoaddr : cmoval W (cconst M) (cvv [_] cconst M).
cmosh : cmoval W (ch VV) (cvv [_] ch VV).
cmovv : cmoval W (cvalid (cvv VV)) VV'
     <- cmoval W (VV W) VV'.

% we need a bunch of canonical-forms lemmata, because of valid
% values. We have a "get" theorem for each canonical form, known to be 
% total when the argument has a certain type

cget_pair  : world -> cval ->  cval -> cval -> type.
%mode cget_pair *W *A *B *C.
cgpair-it : cget_pair _ (cpair V1 V2) V1 V2.
cgpair-vv : cget_pair W (cvalid (cvv VV)) V1 V2
         <- cget_pair W (VV W) V1 V2.

cget_ch    : world -> cval ->  cvval -> type.
%mode cget_ch *W *A *B.
cgch-it : cget_ch _ (ch VV) VV.
cgch-vv : cget_ch W (cvalid (cvv VV)) VV'
       <- cget_ch W (VV W) VV'.

cget_held  : world -> cval ->  cval -> type.
%mode cget_held *W *A *C.
cgheld-it : cget_held _ (cheld W' V) V.
cgheld-vv : cget_held W (cvalid (cvv VV)) V
         <- cget_held W (VV W) V.

cget_pack  : world -> cval ->  world -> cval -> type.
%mode cget_pack *W *A *B *C.
cgpack-it : cget_pack _ (cpack W V) W V.
cgpack-vv : cget_pack W (cvalid (cvv VV))  W' V'
         <- cget_pack W (VV W) W' V'.

cget_wlam  : world -> cval ->  (world -> cval) -> type.
%mode cget_wlam *W *A *B.
cgwlam-it : cget_wlam _ (cwlam V) V.
cgwlam-vv : cget_wlam W (cvalid (cvv VV)) V
         <- cget_wlam W (VV W) V.

cget_const : world -> cval ->  world -> type.
%mode cget_const *W *A *B.
cgconst-it : cget_const _ (cconst W') W'.
cgconst-vv : cget_const W (cvalid (cvv VV)) V
          <- cget_const W (VV W) V.

cget_lam   : world -> cval ->  (cval -> cexp) -> type.
%mode cget_lam *W *A *B.
cglam-it : cget_lam _ (clam C) C.
cglam-vv : cget_lam W (cvalid (cvv VV)) C
        <- cget_lam W (VV W) C.

% stepping relation; indexed by current world
cstep : world -> cexp ->    
        world -> cexp -> type.
%mode cstep *AW *A *BW *B.

cs_fst        : cstep W (cfst VP C)  W (C V) <- cget_pair W VP V _.
cs_snd        : cstep W (csnd VP C)  W (C V) <- cget_pair W VP _ V.
cs_localhost  : cstep W (clocalhost C)  W (C (cconst W)).
cs_lets       : cstep W (clets VH C)  W (C V) <- cget_ch W VH V.
cs_put        : cstep W (cput V C)  W (C VV) <- cmoval W V VV.
cs_leta       : cstep W (cleta VA C)  W (C V) <- cget_held W VA V.
cs_unpack     : cstep W (cunpack VE C)  W (C W' V) <- cget_pack W VE W' V.
cs_wapp       : cstep W (cwapp VL W' C)  W (C (V W')) <- cget_wlam W VL V.
cs_go         : cstep W (cgo W' VA C)  W' C <- cget_const W VA W'.
cs_call       : cstep W (ccall VL V)  W (C V) <- cget_lam W VL C.
% self-transition, as usual
cs_halt       : cstep W chalt  W chalt.

% some canonical forms lemmata
cf_pack : cofv V A W -> ceqtyp A (cexists A') -> cget_pack W V  W' V' -> type.
%mode cf_pack +O +E -W.
- : cf_pack (cov_pack _ _) _ cgpack-it.
- : cf_pack ((cov_valid (covv D)) : cofv _ _ W) EQ (cgpack-vv Z)
 <- cf_pack (D W) EQ Z.

cf_const : cofv V A W -> ceqtyp A (caddr W') -> cget_const W V W' -> type.
%mode cf_const +O +E -W.
- : cf_const cov_const _ cgconst-it.
- : cf_const ((cov_valid (covv D)) : cofv _ _ W) EQ (cgconst-vv W')
 <- cf_const (D W) EQ W'.

cf_wlam : cofv V A W -> ceqtyp A (call A') -> cget_wlam W V V' -> type.
%mode cf_wlam +O +E -L.
- : cf_wlam (cov_wlam _) _ cgwlam-it.
- : cf_wlam ((cov_valid (covv D)) : cofv _ _ W) EQ (cgwlam-vv L)
 <- cf_wlam (D W) EQ L.

cf_lam : cofv V A W -> ceqtyp A (A' ccont) -> cget_lam W V C -> type.
%mode cf_lam +O +E -L.
- : cf_lam (cov_lam _) _ cglam-it.
- : cf_lam ((cov_valid (covv D)) : cofv _ _ W) EQ (cglam-vv L)
 <- cf_lam (D W) EQ L.

cf_held : cofv V A W -> ceqtyp A (A' cat W') -> cget_held W V  V' -> type.
%mode cf_held +O +E -H.
- : cf_held (cov_held _) _ cgheld-it.
- : cf_held ((cov_valid (covv D)) : cofv _ _ W) EQ (cgheld-vv H)
 <- cf_held (D W) EQ H.

cf_pair : cofv V A W -> ceqtyp A (A1 c& A2) -> cget_pair W V  V1 V2 -> type.
%mode cf_pair +O +E -P.
- : cf_pair (cov_pair _ _) _ cgpair-it.
- : cf_pair ((cov_valid (covv D)) : cofv _ _ W) EQ (cgpair-vv P)
 <- cf_pair (D W) EQ P.

cf_ch : cofv V A W -> ceqtyp A (csh Af) -> cget_ch W V VV -> type.
%mode cf_ch +O +E -P.
- : cf_ch (cov_ch _) _ cgch-it.
- : cf_ch ((cov_valid (covv D)) : cofv _ _ W) EQ (cgch-vv VV)
 <- cf_ch (D W) EQ VV.

% lemma: if a value is of mobile type, then it is a
% mobile value (like the canonical forms lemmas above)

cmobmov : cmobile A -> cofv V A W -> cmoval W V V' -> type.
%mode cmobmov +MOB +WV -MOV.

- : cmobmov cmob_at _ cmoat.
- : cmobmov MA WA MOA ->
    cmobmov MB WB MOB ->
    cmobmov (cmob_& MA MB) (cov_pair WA WB) (cmopair MOA MOB).
- : cmobmov cmob_addr _ cmoaddr.
- : cmobmov (cmob_all MF) (cov_wlam WF) (cmoall MOF)
 <- ({w} cmobmov (MF w) (WF w) (MOF w)).
- : cmobmov (cmob_exists MF) (cov_pack _ WF : cofv (cpack W' _) _ _) (cmoexists MOF)
 <- cmobmov (MF W') WF MOF.
- : cmobmov cmob_sh (cov_ch WVV) cmosh.
- : cmobmov (MOB : cmobile (Af W)) (cov_valid (covv (D : {w} cofv (Vf w) (Af w) w)) : cofv _ (Af W) W) 
            (cmovv MOV : cmoval W _ _)
 <- cmobmov MOB (D W) MOV.

% some "inversion" lemmata for preservation
ci_ch : cofv V A W -> ceqtyp A (csh Af) -> cget_ch W V VV -> cofvv VV Af -> type.
%mode ci_ch +O +E +P -D.
- : ci_ch (cov_ch D) _ cgch-it D.
- : ci_ch ((cov_valid (covv D)) : cofv _ _ W) EQ (cgch-vv VV) DD
 <- ci_ch (D W) EQ VV DD.

ci_pair : cofv V A W -> ceqtyp A (A1 c& A2) -> cget_pair W V  V1 V2 -> 
          cofv V1 A1 W -> cofv V2 A2 W -> type.
%mode ci_pair +O +E +P -V1 -V2.
- : ci_pair (cov_pair D1 D2) _ cgpair-it D1 D2.
- : ci_pair ((cov_valid (covv D)) : cofv _ _ W) EQ (cgpair-vv P) D1 D2
 <- ci_pair (D W) EQ P D1 D2.

ci_lam : cofv V A W -> ceqtyp A (A' ccont) -> cget_lam W V C ->
         ({x} cofv x A' W -> cof (C x) W) -> type.
%mode ci_lam +O +E +L -D.
- : ci_lam (cov_lam D) _ cglam-it D.
- : ci_lam ((cov_valid (covv D)) : cofv _ _ W) EQ (cglam-vv L) DD
 <- ci_lam (D W) EQ L DD.

ci_wlam : cofv V A W -> ceqtyp A (call A') -> cget_wlam W V V' ->
          ({w} cofv (V' w) (A' w) W) -> type.
%mode ci_wlam +O +E +L -D.
- : ci_wlam (cov_wlam D) _ cgwlam-it D.
- : ci_wlam ((cov_valid (covv D)) : cofv _ _ W) EQ (cgwlam-vv L) D'
 <- ci_wlam (D W) EQ L D'.

ci_pack : {A': world -> ctyp} cofv V A W -> ceqtyp A (cexists A') ->
          cget_pack W V  W' V' -> cofv V' (A' W') W -> type.
%mode ci_pack +O +A' +E +P -B.
- : ci_pack A' (cov_pack A' B : cofv (cpack W' V') (cexists A') W) EQ
     (cgpack-it : cget_pack W (cpack W' V')  W' V') B.
- : ci_pack A' ((cov_valid (covv D)) : cofv _ _ W) EQ (cgpack-vv Z) B
 <- ci_pack A' (D W) EQ Z B.

ci_held : cofv V A W -> ceqtyp A (A' cat W') -> cget_held W V  V' -> 
	  cofv V' A' W' -> type.
%mode ci_held +O +E +H -V.
- : ci_held (cov_held D) _ cgheld-it D.
- : ci_held ((cov_valid (covv D)) : cofv _ _ W) EQ (cgheld-vv H) DD
 <- ci_held (D W) EQ H DD.

% well-typed mobile values are valid values
cmomo : cmoval W V V' -> cofv V A W -> cofvv V' ([w] A) -> type.
%mode cmomo +MV +OV -OV'.

- : cmomo (cmovv MOB) (cov_valid (covv UV)) WVV
 <- cmomo MOB (UV W) WVV.
- : cmomo cmosh (cov_ch WVV) (covv [_] cov_ch WVV).
- : cmomo cmoaddr cov_const (covv [_] cov_const).
- : cmomo cmoat (cov_held M) (covv [_] cov_held M).
- : cmomo (cmopair MV1 MV2) (cov_pair WV1 WV2)
           (covv [w] cov_pair (cov_valid D1) (cov_valid D2))
 <- cmomo MV1 WV1 D1
 <- cmomo MV2 WV2 D2.
- : cmomo (cmoall MV) (cov_wlam WV) (covv [w'] cov_wlam ([w] cov_valid (D w)))
 <- ({w} cmomo (MV w) (WV w) (D w)).
- : cmomo (cmoexists MV) (cov_pack Af WF) (covv [w'] cov_pack Af (cov_valid F))
 <- cmomo MV WF F.

% progress
cprog : cof C W -> cstep W C  W' C' -> type.
%mode cprog +COF -CSTEP.

- : cprog co_halt cs_halt.
- : cprog (co_fst WV _) (cs_fst G) <- cf_pair WV ceqtyp_ G.
- : cprog (co_snd WV _) (cs_snd G) <- cf_pair WV ceqtyp_ G.
- : cprog (co_call WV _) (cs_call G) <- cf_lam WV ceqtyp_ G.
- : cprog (co_localhost _) cs_localhost.
- : cprog (co_unpack WV _) (cs_unpack G) <- cf_pack WV ceqtyp_ G.
- : cprog (co_wapp WV _) (cs_wapp G) <- cf_wlam WV ceqtyp_ G.
- : cprog (co_leta WV _) (cs_leta G) <- cf_held WV ceqtyp_ G.
- : cprog (co_lets WV _) (cs_lets G) <- cf_ch WV ceqtyp_ G.
- : cprog (co_put MOB WV _) (cs_put CM) <- cmobmov MOB WV CM.
- : cprog (co_go WV _) (cs_go G) <- cf_const WV ceqtyp_ G.

% preservation
cpresv : cof C W -> cstep W C  W' C' -> cof C' W' -> type.
%mode cpresv +COF +CSTEP -COF'.

- : cpresv co_halt cs_halt co_halt.
- : cpresv (co_go WV WC) (cs_go G) WC.
- : cpresv (co_fst WV WC) (cs_fst G) (WC _ WV1) <- ci_pair WV ceqtyp_ G WV1 _.
- : cpresv (co_snd WV WC) (cs_snd G) (WC _ WV2) <- ci_pair WV ceqtyp_ G _ WV2.
- : cpresv (co_call WVF WVA) (cs_call G) (WC _ WVA) <- ci_lam WVF ceqtyp_ G WC.
- : cpresv (co_wapp WV WC : cof (cwapp _ W' _) W) (cs_wapp G) (WC _ (WV' W')) 
 <- ci_wlam WV ceqtyp_ G WV'.
- : cpresv (co_unpack WV WC) (cs_unpack (G : cget_pack _ _ W' V)) (WC W' V WV') 
 <- ci_pack _ WV ceqtyp_ G WV'.
- : cpresv (co_leta WA WC) (cs_leta G) (WC _ WV) <- ci_held WA ceqtyp_ G WV.
- : cpresv (co_put MOB WV WC) (cs_put CM) (WC _ WVV) <- cmomo CM WV WVV.
- : cpresv (co_lets WV WC) (cs_lets G) (WC _ WVV) <- ci_ch WV ceqtyp_ G WVV.
- : cpresv (co_localhost WC) cs_localhost (WC _ cov_const).

% These are required because of overconservativity of Twelf's world subsumption (I think) 
% in TOCPS. These are strange, but don't hurt anything.
% This is our last chance to declare worlds before they are auto-frozen by the totality
% assertions below.
%worlds (blockw | blockcv | blockcvv) (cval) (cexp) (cvval).
%worlds (blockw | blockwcv | blockwcvv) (cofv _ _ _) (cof _ _) (cofvv _ _) (cmobile _).

%worlds (blockw) (cf_pair _ _ _) (cf_held _ _ _) (cf_lam _ _ _) (cf_wlam _ _ _) (cf_const _ _ _) 
 (cf_pack _ _ _) (cf_ch _ _ _) (ci_held _ _ _ _) (ci_pair _ _ _ _ _) (ci_lam _ _ _ _) (ci_wlam _ _ _ _)
 (ci_pack _ _ _ _ _) (ci_ch _ _ _ _) (cmobmov _ _ _) (cmomo _ _ _) (cprog _ _) (cpresv _ _ _).

%total D (cmobmov _ D _).
%total D (cmomo D _ _).
%total D (ci_ch D _ _ _).
%total D (ci_held D _ _ _).
%total D (ci_pack _ D _ _ _).
%total D (ci_pair D _ _ _ _).
%total D (ci_lam D _ _ _).
%total D (ci_wlam D _ _ _).
%total D (cf_pack D _ _).
%total D (cf_pair D _ _).
%total D (cf_ch D _ _).
%total D (cf_held D _ _).
%total D (cf_lam D _ _).
%total D (cf_wlam D _ _).
%total D (cf_const D _ _).
%total D (cprog D _).
%total D (cpresv _ D _).
