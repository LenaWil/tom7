let
  import "tinyworld.aah"
  import "list.aah"
  import "string.aah"

  val PERPAGE = 18

  val url = limit(?/, cgiurl ())
  val name = tr("a-z0-9", "/cd/", head url)

  fun padto n s =
      let fun f s =
          if size s >= n
          then s
          else f [[s] ]
      in
          f s
      end

  val padto16 = padto 16

  (* Puts a . on it, then pads to 17 chars *)
  fun padlev s = padto 17 [[s].]

  fun rep 0 s = []
    | rep n s = [[s][rep (n - 1) s]]

in
  contenttype [text/xml];

  if size name = 0 orelse size name > 16
  then die [illegal name]
  else ();

  if match (re "all[0-9]+", name)
  then 
     let
         val n = stoi (substr (name, 3, size name - 3))
         val ns = itos n

         val levs = 
             select (name, modifydate, modifyip)
             from tinyworld : level
             order by modifydate desc
             limit {n * PERPAGE}, {PERPAGE}
         val nlevs = list-length levs

         val up =
             if n = 0
             then [#  T                                   #]
             else [#  T  ? ?@[padlev [all[(itos (n - 1))]]]        up! #]

         fun onelev (s, d, i) =
             [#     ? ?@[padlev s]            #]

         val down =
             if list-length levs < PERPAGE
             then [#                                      #]
             else [#     ? ?@[padlev [all[(itos (n + 1))]]]        dn! #]

         val lstr = string-concat (list-map onelev levs)
         val nstr = rep (PERPAGE - nlevs) [#       NOTHING TO SEE HERE            #]
     in
print    
[########################################\
 # RECENT LEVELS, PAGE [padto16 ns    ] #\
 #                    .? (?(T=?( .      #\
 ########################################\
 [up]\
 [lstr]\
 [nstr]\
 [down]\
 ########################################\
 Y] (* locked *)

     end
  else
  case select (contents, locked)
       from tinyworld : level
       where name = {name} limit {1} of
    (contents, locked) :: _ => 
         let in
             print contents;
             (if locked
              then print [Y]
              else print [N])
         end
   | nil =>
print
(* XXX put the level name inside the
   default contents *)
[########################################\
 # [padto16 name  ]     #?T>H@tutorial8.#\
 ########################################\
 #                                      #\
 #                                      #\
 #                                      #\
 # 404 NOT FOUND                        #\
 #                                      #\
 # this level does not exist yet        #\
 #                                      #\
 # you can create it by pressing tab    #\
 # to enter the editor and save with    #\
 # ctrl s                               #\
 #                                      #\
 # then everyone can enjoy it           #\
 #                                      #\
 #                                      #\
 #                                      #\
 #                                      #\
 #                                      #\
 ########                               #\
 #    ###                               #\
 # T   H#  or go to the menu...         #\
 #    ###                               #\
 ########################################\
 N] (* locked *)

end