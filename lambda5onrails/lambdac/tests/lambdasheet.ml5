unit
    import "std.mlh"
    import "list.mlh"
    import "string.mlh"
    import "dom.mlh"
    import "timer.mlh"
    import "trivialdb.mlh"
    import "lock.mlh"
    import "regexp.mlh"

    put WIDTH = 12
    put HEIGHT = 12

    do runtime.no-messages ()

    (* really simple expressions, for now *)
    datatype value =
         Int of int
       | String of string

    datatype exp =
         Plus of exp * exp
       | Times of exp * exp
       | Val of value
      (* XXX vars *)

    fun cellid (x, y) = [c[itos x]_[itos y]]
    fun inline app-cells f =
        let 
            fun cells (y, x) =
                if x >= WIDTH
                then ()
                else (f (x, y);
                      cells (y, x + 1))
            fun rows y =
                if y >= HEIGHT
                then ()
                else (cells (y, 0); rows (y + 1))
        in
            rows 0
        end

    do dom.setstring 
        (dom.getbyid [page],
         [innerHTML],
         [please wait.])

    fun cell-key cell ?\r = 
        let put cid = cellid cell
            put v = dom.getstring(dom.getbyid cid, [value])
        in
            from server get
            (* alert [pretending to save [cellid cell]] *)
            trivialdb.update (cid, v)
        end
      | cell-key cell c = ()
        (* could color this as unsaved? *)

    val table =
     let fun makecells (y, x) =
           if x >= WIDTH
           then ""
           else [<td><input class="cell" type="text" id="[cellid (x, y)]"
                   onblur="[say cell-key (x, y) ?\r]"
                   onkeyup="[say { event.keyCode = c } cell-key (x, y) c]"/>\
                 </td>[makecells (y, x + 1)]]
         fun makerows y =
           if y >= HEIGHT
           then ""
           else [<tr>[makecells (y, 0)]</tr>\n[makerows (y + 1)]]
     in
         [<table style="spreadsheet">
          [makerows 0]
          </table>]
     end

    do dom.setstring 
        (dom.getbyid [page],
         [innerHTML],
         [<style>[datafile "lambdasheet.css"]</style>
          [table]])

    fun updated-cell (x, y, v) = 
        (* and blink its color? *)
        dom.setstring (dom.getbyid (cellid (x, y)),
                       "value",
                       v)

    (* and update handlers *)
    do from server get
        app-cells (fn (x, y) => 
                   let val cid = cellid (x, y)
                       put (x, y) = (x, y)
                       fun doup () =
                           let put v = trivialdb.read cid
                           in from home get
                               updated-cell (x, y, v)
                           end
                   in
                       if trivialdb.read cid seq ""
                       then ()
                       else doup ();
                       trivialdb.addhook(cid, cont doup)
                   end) (* XXX *)

in
end