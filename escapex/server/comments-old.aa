
let
    import "escape.aah"
    import "cgi.aah"

    (* XXX allow "simplified" output for browsing with escape client *)

    val lim = 25
    val start = cgigetnum("start", fn _ => 0)

    val allow-spoilers = 1 = cgigetnum("spoilers", fn _ => 0)
    val no-info = 1 = cgigetnum("noinfo", fn _ => 0)

    val pass = cgigetstring ([pass], K "")
    val admin = pass seq ESCAPE-ADMINPASS

    do  admin andthen adminheader [Administrating Comments] 

    val coms = 
	select (id, of, byuser, date, spoiler, comment)
	from escape : lcomment
	where any {if allow-spoilers
		   then "1=1"
		   else "spoiler='f'"}
	  and any {if no-info
		   then "info='f'"
		   else "1=1"}
	order by id desc
	limit {start}, {lim}

    fun level n s =
	[<b><a href="[FCGI]/escape/level/[itos n][if
	 allow-spoilers then [?spoilers=1]
	 else []]">[colorize s]</a></b>]

    fun pcomment (id, par, byuser, date, spoiler, comment) =
	let 
	    val (title, md) = get escape : level (par) : (title, md)
	    (* PERF sensible? It's only an extra 'stat' for a
               file the web server is going to open anyway... *)
	    val _ = make-screenshot md
	in
	    print 
	    [<tr>
	     <td bgcolor="#DDDDFF">[itos id]</td>
	     <td bgcolor="#DDDDFF"><b>[level par title]</b></td>
	     <td bgcolor="#EEEEFF">[get escape : user (byuser) : name] ([itos byuser])</td>
	     <td bgcolor="#EEEEFF">[datefmt ("%a %d %b %Y %H:%M", date)]</td>
	     <td bgcolor="#EEEEFF">[if spoiler then [<b>SPOILER</b>]
				    else [&nbsp;]]
	     [if admin then [<br>del(<a href="[FCGI]/escape/delcomment?n=[itos id]&pass=[pass]">!</a>)]
		       else []]
	     </td></tr>
	     <tr>
	     <td>&nbsp;</td>
	     <td colspan=4><table width="100%">
	       <td valign=top>
               [level par [<img src="[PNGWEB][md].png" border=0>]]</td>
	       <td valign=top width="100%">[mkcomment comment]</td></tr>
	     </table></td>
	     </tr>
	     <tr><td>&nbsp;</td></tr>\n]
	end

    fun bar () =
    print [<tr><td colspan=4 bgcolor="#DDFFEE"><a href="/">&laquo; home</a> &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; <b>Browsing all comments</b>
	   (spoilers: <b><a href="[FCGI]/escape/comments?spoilers=1&noinfo=[if no-info then [1] else [0]]">on</a> | <a href="[FCGI]/escape/comments?spoilers=0&noinfo=[if no-info then [1] else [0]]">off</a></b>) (info: <b><a href="[FCGI]/escape/comments?spoilers=[if allow-spoilers then [1] else [0]]">on</a> | <a href="[FCGI]/escape/comments?spoilers=[if allow-spoilers then [1] else [0]]&noinfo=1">off</a></b>)
	   </td>
	   <td bgcolor="#DDFFEE"><a href="comments?start=[itos (start + 25)][
	   if allow-spoilers then [&spoilers=1] else []][
           if no-info then [&noinfo=1] else []]">next 25</a></td>
	   </tr>
	   <tr><td colspan=5 bgcolor="#99DD99" height=2><img src="/spacer.gif" width=1 height=1></td></tr>
	   <tr><td colspan=5 bgcolor="#FFFFFF" height=2><img src="/spacer.gif" width=2 height=1></td></tr>]

in
    htmlhead "Escape: Most recent comments";
    print "<table width=600 cellspacing=0 cellpadding=2>\n";

    bar ();
    app (coms, pcomment);
    bar ();
	   
    print "</table>\n"
end